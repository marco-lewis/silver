?start: (func_spec)* -> specs

?func_spec: func "(" defs ")" "->" "(" defs ")" pre post -> funcspec

?pre: "pre" "{" stmts "}" -> pre

?post: "post" "{" stmts "}" -> post

?func: NAME

?defs: (def ("," def)*)?
?def: "define" NAME ":" type    -> definition

?type: NAT                              -> nat
    | "{" "0" "," "1" "}"               -> bool
    | "{" "0" "," "1" "}" "^" NUMBER    -> int
    | type ("->" type)+                 -> function

?stmts: (stmt)*
?stmt: def 
| "assert" "(" lexpr ")"        -> assertion

?lexpr: numexpr
    | "(" lexpr ")"
    | numexpr "=" numexpr       -> eq
    | numexpr ">" numexpr       -> gt
    | numexpr "<" numexpr       -> lt
    | numexpr ">=" numexpr      -> ge
    | numexpr "<=" numexpr      -> le
    | lexpr "&" lexpr           -> land
    | lexpr "|" lexpr           -> lor
    | lexpr "->" lexpr          -> implies
    | lexpr "<->" lexpr         -> equiv
    | "¬" lexpr                 -> lnot
    | "@" NAME "." lexpr        -> forall
    | "$" NAME "." lexpr        -> exists

?numexpr: NUMBER
    | NAME                      -> var
    | "(" numexpr ")"
    | "-" numexpr               -> neg
    | numexpr "+" numexpr       -> add
    | numexpr "-" numexpr       -> sub
    | numexpr "*" numexpr       -> mul
    | numexpr "/" numexpr       -> div
    | numexpr "^" numexpr       -> pow
    | func "(" args ")"         -> call
    | "SUM" "[" NAME "]" "(" numexpr ")"    -> sum

?args: (arg ("," arg)*)?        -> args
?arg: NAME

NAT: "N" | "ℕ"

%import common.CNAME -> NAME
%import common.NUMBER
%import common.WS
%import common.SH_COMMENT

%ignore WS
%ignore SH_COMMENT